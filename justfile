name := 'clapgrep'
appid := 'de.leopoldluley.Clapgrep'
frontend := 'clapgrep-gnome'

rootdir := ''
prefix := '/usr'

base-dir := absolute_path(clean(rootdir / prefix))

bin-src := 'target' / 'release' / frontend
bin-dst := base-dir / 'bin' / name

desktop := appid + '.desktop'
desktop-src := 'assets' / desktop
desktop-dst := base-dir / 'share' / 'applications' / desktop

metainfo := appid + '.metainfo.xml'
metainfo-src := 'assets' / metainfo
metainfo-dst := base-dir / 'share' / 'metainfo' / metainfo

icons-src := 'assets' / 'icons' / 'hicolor'
icons-dst := base-dir / 'share' / 'icons' / 'hicolor'

icon-svg-src := icons-src / 'scalable' / 'apps' / appid + '.svg'
icon-svg-dst := icons-dst / 'scalable' / 'apps' / appid + '.svg'

po-src := 'assets' / 'locale'
po-dst := base-dir / 'share' / 'locale'

clean:
  cargo clean

build *args: build-translations
  cargo build --package {{frontend}} {{args}}

build-offline *args: build-translations
  cargo --offline fetch --manifest-path Cargo.toml --verbose
  cargo --offline build --package {{frontend}} {{args}}

check *args:
  cargo clippy --all-features {{args}}

run *args: build-translations
  env RUST_BACKTRACE=full cargo run --package {{frontend}} {{args}}

ci: setup-flatpak-repos
  flatpak-builder --keep-build-dirs --disable-updates --build-only --ccache --force-clean flatpak build-aux/{{appid}}.json
  echo Check formatting:
  ./build-aux/fun.sh env CARGO_HOME=/run/build/clapgrep/cargo cargo --offline fmt --all -- --check --verbose
  echo Check code:
  ./build-aux/fun.sh env CARGO_HOME=/run/build/clapgrep/cargo cargo --offline check
  echo Check code with Clippy:
  ./build-aux/fun.sh env CARGO_HOME=/run/build/clapgrep/cargo cargo --offline clippy --workspace --all-targets --all-features -- -D warnings

install:
  mkdir -p {{po-dst}}
  install -Dm0755 {{bin-src}} {{bin-dst}}
  install -Dm0755 {{desktop-src}} {{desktop-dst}}
  install -Dm0755 {{metainfo-src}} {{metainfo-dst}}
  install -Dm0755 {{icon-svg-src}} {{icon-svg-dst}}
  cp -r {{po-src}} {{po-dst}}

make-makefile:
  echo "# This file was generated by 'just make-makefile'" > build-aux/Makefile
  echo ".PHONY: all" >> build-aux/Makefile
  echo "all:" >> build-aux/Makefile
  just -n build-offline --release 2>&1 | sed 's/^/\t/' | sed 's/\$/$$/g' >> build-aux/Makefile
  just -n --set prefix /app install 2>&1 | sed 's/^/\t/' >> build-aux/Makefile

prepare-flatpak: make-makefile
  python3 build-aux/flatpak-cargo-generator.py ./Cargo.lock -o build-aux/cargo-sources.json

install-flatpak: setup-flatpak-repos
  flatpak-builder flatpak-build gnome/de.leopoldluley.Clapgrep.json --force-clean --install --user

setup-flatpak-repos:
	flatpak remote-add --user --if-not-exists flathub https://dl.flathub.org/repo/flathub.flatpakrepo
	flatpak install --or-update --user --noninteractive flathub org.gnome.Platform//46 org.gnome.Sdk//46 org.freedesktop.Sdk.Extension.rust-stable//23.08

gettext *args:
  xgettext \
    --from-code=UTF-8 \
    --add-comments \
    --keyword=_ \
    --keyword=C_:1c,2 \
    --language=C \
    --output=po/messages.pot \
    --files-from=po/POTFILES \
    {{args}}
  cat po/LINGUAS | while read lang; do \
    msgmerge -N -U po/$lang.po po/messages.pot; \
    rm -f po/$lang.po~; \
  done

add-translation language:
  msginit -l {{language}}.UTF8 -o po/{{language}}.po -i po/messages.pot

build-translations:
  cat po/LINGUAS | while read lang; do \
    mkdir -p assets/locale/$lang/LC_MESSAGES; \
    msgfmt -o assets/locale/$lang/LC_MESSAGES/{{appid}}.mo po/$lang.po; \
  done
